import io.getstream.video.android.Configuration
import io.getstream.video.android.Dependencies

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.squareup.wire'
    id 'binary-compatibility-validator'
    id 'io.getstream.video.generateServices'
}

ext {
    PUBLISH_GROUP_ID = Configuration.artifactGroup
    PUBLISH_ARTIFACT_ID = 'stream-video-android'
    PUBLISH_VERSION = rootVersionName
}

apply from: "${rootDir}/scripts/publish-module.gradle"

wire {
    kotlin {
        rpcRole = 'none'
    }

    protoPath {
        srcDir 'src/main/proto'
    }
}

generateRPCServices {}

android {
    compileSdk Configuration.compileSdk

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    defaultConfig {
        minSdk Configuration.minSdk
        targetSdk Configuration.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles "consumer-rules.pro"
        }

        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles "consumer-rules.pro"
        }
    }

    resourcePrefix = "stream_video_"

    sourceSets {
        main.java.srcDirs += "build/generated/source/services"
    }


    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    api Dependencies.streamWebRTC
    api Dependencies.streamWebRTCUI

    implementation Dependencies.androidxCore
    implementation Dependencies.androidxAppcompat
    implementation Dependencies.material

    // lifecycle
    implementation Dependencies.androidxLifecycleRuntime
    implementation Dependencies.androidxLifecycleProcess
    implementation Dependencies.androidxLifecycleViewModel

    // coroutines
    implementation Dependencies.coroutines

    // API & Protobuf
    api Dependencies.wireRuntime
    implementation Dependencies.retrofit
    implementation Dependencies.retrofitWireConverter
    implementation Dependencies.okhttpLoggingInterceptor
    implementation Dependencies.kotlinSerializationJson
    implementation Dependencies.retrofitMoshi
    implementation Dependencies.retrofitScalars

    implementation Dependencies.moshi
    implementation Dependencies.moshiKotlin
    implementation Dependencies.moshiAdapters

    // Stream
    implementation Dependencies.streamLog
    implementation Dependencies.streamPush
    implementation Dependencies.streamPushDelegate

    // Unit Tests
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'androidx.test.ext:junit:1.1.5'
    testImplementation 'org.robolectric:robolectric:4.9'
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

