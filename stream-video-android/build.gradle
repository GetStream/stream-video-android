import io.getstream.video.android.Configuration
import io.getstream.video.android.Dependencies
import io.getstream.video.android.Versions

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.squareup.wire'
    id 'binary-compatibility-validator'
    id 'com.google.gms.google-services'
}

wire {
    kotlin {
        rpcRole = 'none'
    }

    protoPath {
        srcDir 'src/main/proto'
        // TODO -> check proto docs to generate a library instead of files directly
    }
}

android {
    compileSdk Configuration.compileSdk

    defaultConfig {
        minSdk Configuration.minSdk
        targetSdk Configuration.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    lintOptions {
        abortOnError false
    }

    composeOptions {
        kotlinCompilerExtensionVersion Versions.COMPOSE_COMPILER
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-Xexplicit-api=strict", "-Xjvm-default=enable"]
}

dependencies {
    implementation Dependencies.androidxCore
    implementation Dependencies.androidxAppcompat
    implementation Dependencies.material

    // lifecycle
    implementation Dependencies.androidxLifecycleRuntime
    implementation Dependencies.androidxLifecycleProcess

    // coroutines
    implementation Dependencies.coroutines

    // API & Protobuf
    api Dependencies.wireRuntime
    implementation Dependencies.retrofit
    implementation Dependencies.retrofitWireConverter
    implementation Dependencies.okhttpLoggingInterceptor
    implementation Dependencies.kotlinSerializationJson

    // A/V Calls
    implementation Dependencies.liveKit
    api Dependencies.webRTC

    // Compose
    implementation Dependencies.composeRuntime
    implementation Dependencies.composeUi
    implementation Dependencies.composeUiTooling
    implementation Dependencies.composeFoundation
    implementation Dependencies.composeMaterial
    implementation Dependencies.activityCompose
    implementation Dependencies.composeIconsExtended

    // Notifications
    implementation platform('com.google.firebase:firebase-bom:30.3.1')
    implementation "com.google.firebase:firebase-messaging"

    // Unit Tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}