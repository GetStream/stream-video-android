import io.getstream.video.android.Configuration
import io.getstream.video.android.Dependencies
import io.getstream.video.android.Versions

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk Configuration.compileSdk

    defaultConfig {
        applicationId "io.getstream.video.android.tutorial_final"
        minSdk Configuration.minSdk
        targetSdk Configuration.targetSdk
        versionCode Configuration.versionCode
        versionName Configuration.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        benchmark {
            signingConfig signingConfigs.debug
            matchingFallbacks = ['release']
            debuggable false
        }
    }

    File envProps = rootProject.file(".env.properties")
    if (envProps.exists()) {
        Properties properties = new Properties()
        properties.load(new FileInputStream(envProps))
        buildTypes.each { buildType ->
            properties.findAll { "${it.key}".startsWith("SAMPLE_")}
                    .each {
                        buildType.buildConfigField("String", "${it.key}", "\"${it.value}\"")
                    }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion Versions.COMPOSE_COMPILER
    }

    lint {
        baseline = file("lint-baseline.xml")
    }
}

dependencies {
    implementation project(":stream-video-android-compose")

    implementation Dependencies.androidxCore
    implementation Dependencies.androidxAppcompat
    implementation Dependencies.material
    implementation Dependencies.androidxLifecycleRuntime

    // Compose
    implementation platform(Dependencies.composeBom)
    implementation Dependencies.composeRuntime
    implementation Dependencies.composeUi
    implementation Dependencies.composeUiTooling
    implementation Dependencies.composeFoundation
    implementation Dependencies.composeMaterial
    implementation Dependencies.activityCompose
    implementation Dependencies.composeIconsExtended

    // Stream Logger
    implementation Dependencies.streamLog
    implementation Dependencies.streamLogAndroid
}