apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
    )
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                tasks.named("generateMetadataFileForReleasePublication").configure { dependsOn("androidSourcesJar") }
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Stream Video official Android SDK'
                    url = 'https://github.com/getstream/stream-video-android'
                    licenses {
                        license {
                            name = 'Stream License'
                            url = 'https://github.com/GetStream/stream-video-android/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'filbabic'
                            name = 'Filip Babic'
                            email = "filip.babic@getstream.io"
                            url = "https://github.com/filbabic"
                        }
                        developer {
                            id = 'skydoves'
                            name = 'Jaewoong Eum'
                            email = "jaewoong.eum@getstrea.io"
                            url = "https://github.com/skydoves"
                        }
                        developer {
                            id = 'kanat'
                            name = 'Kanat Kiialbaev'
                            email = "kanat.kiialbaev@getstream.io"
                            url = "https://github.com/kanat"
                        }
                        developer {
                            id = 'TGazica'
                            name = 'Tomislav Gazica'
                            email = "tomislav.gazica@getstream.io"
                            url = "https://github.com/TGazica"
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/getstream/stream-video-android.git'
                        developerConnection = 'scm:git:ssh://github.com/getstream/stream-video-android.git'
                        url = 'https://github.com/getstream/stream-video-android/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
